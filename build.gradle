// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: './scripts/versions.gradle'


    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_gradle_plugin"

        //Nav SafeARGS
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"

        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"

        //Dagger Hilt
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.hilt"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def localProperties = new Properties()
if(project.file("local.properties").exists()){
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/EYDS-CA/BCVAX-Android")
            credentials {
                username = localProperties['USER'] ?: System.getenv("USER")
                password = localProperties['TOKEN'] ?: System.getenv("TOKEN")
            }
        }
        jcenter()
    }
    apply plugin: "com.diffplug.spotless"
}


spotless {

    kotlin {
        target '**/*.kt'
        ktlint()
        trimTrailingWhitespace()
        indentWithSpaces()
    }

    kotlinGradle {
        target '*.gradle.kts'
        ktlint()
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        indentWithSpaces()
        trimTrailingWhitespace()
    }

    format 'xml', {
        target '**/*.xml'
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}